apply plugin: 'com.android.application'

android {
    compileSdkVersion 28
    buildToolsVersion "29.0.0"

    defaultConfig {
        applicationId "com.handheld.uhfr"
        minSdkVersion 19
        targetSdkVersion 28
        versionCode 14
        versionName "2.0.5"

    }

    applicationVariants.all { variant ->
        variant.outputs.all {
            def fileName = "uhfr_v${defaultConfig.versionName}.apk"
            outputFileName = fileName
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
        }
    }
}

task clearJar(type: Delete) {
    delete 'build/libs/smarter.jar'
    delete 'libs/smarter.jar'
}

//打包任务
task makeJar(type: Jar) {
    //指定生成的jar名
    baseName 'uhfr_v1.9'
    //从哪里打包class文件
    from "build/intermediates/javac/debug/classes/"
    //去掉不需要打包的目录和文件
    exclude('BuildConfig.class', 'R.class')
    //去掉R$开头的文件
    exclude { it.name.startsWith('R$'); }
    destinationDir(file('build/libs'))
    exclude('/com/handheld/uhfr/BuildConfig.class')
    exclude('/com/handheld/uhfr/UHFRManagerBank.class')
    exclude('/com/handheld/uhfrdemo')
    //此段代码将相关信息写到META-INF文件中的MANEFEST.MF文件
    manifest {
        attributes(
                //'Implementation-Title': "${project.name}",
                //'Implementation-Version': "${version}",
                'Implementation-Title': "uhfr",
                'Implementation-Version': "2.0",
                'Built-Date': new Date().getDateTimeString(),
                'Built-With':
                        "gradle-${project.getGradle().getGradleVersion()},groovy-${GroovySystem.getVersion()}",
                'Created-By':
                        'Java ' + System.getProperty('java.version') + ' (' + System.getProperty('java.vendor') + ')')
    }

}
makeJar.dependsOn(clearJar, build)

dependencies {
    implementation 'com.android.support:support-v4:28.0.0'
    implementation files('libs/jxl.jar')
    implementation files('libs/ModuleAPI_J.jar')
}
